{$CLEO .cs}
0000:


REPEAT
    WAIT 0
UNTIL 0AFA:  SAMP_IS_READY 

0B6D: 31@ CREATE_FONT "Arial Black" HEIGHT 10 FLAGS 0x5
0BFC: set_global_var "font" = 31@

0B34: "busdriver" @VehicleRecording
0B34: "vloop" @VehicleRecordingLoop
0B34: "startbusdriver" @VehicleRecordingLoop
0B34: "debugdesiredspeed" @DebugDesiredSpeed

0AF8: "AUTO Bus Driver: Foloseste {FFFF00}/busdriver{FFFFFF} pentru mai multe informatii." -1
0AF8: "Mod creat de RewardK (in 2017) actualizat in 2026. Functioneaza doar pe serverul bugged.ro." -1

$WAIT_FOR_CASH = FALSE
$DEBUG_DESIRED_SPEED = FALSE

WHILE TRUE
    WAIT 0
    IF
    00DF:   actor $PLAYER_ACTOR driving
    THEN       
        // dezactivata functia de record (inutila, posibil cauzatoare de accidente)    
        /* IF AND
        8B21: NOT_CHAT_OPEN
        0AB0: 122 {O}
        THEN
            REPEAT
                WAIT 0
            UNTIL  8AB0: 122 {O} //103
            32@ = 0
            IF
            0AAB:   file_exists "CLEO\VehicleRecording.path"
            THEN
                0B00: delete_file "CLEO\VehicleRecording.path"
            END           
            0AF8: "{FFFF00}Recorder:{FFFFFF} recording has been started." -1
            03C0: 0@ = actor $PLAYER_ACTOR car
            0407: 1@ 2@ 3@ CAR 0@ w_OFFSET 0.0 0.0 0.0  
            018A: 29@ = create_checkpoint_at 1@ 2@ 3@
 
            $WAIT_FOR_CASH = FALSE

            0A9A: 31@ = openfile "CLEO\VehicleRecording.path" mode "wb+" 
            
            
            REPEAT
                WAIT 0
                IF
                80DF:   actor $PLAYER_ACTOR driving
                THEN
                    0AF8: "{FF0000}Recorder:{FFFFFF} you have crashed. Recodring stopped." -1
                    BREAK
                END
                0AB1: @DRAW_TEXT_RECORDING 0
                IF
                32@ > 100
                THEN
                    32@ = 0
                    03C0: 0@ = actor $PLAYER_ACTOR car
                    0407: 1@ 2@ 3@ CAR 0@ w_OFFSET 0.0 0.0 0.0  
                    02E3: 4@ = car 0@ speed
                    0A9E: writefile 31@ size 16 from 1@ 
                    018C: play_sound 1052 at 0.0 0.0 0.0
                END    
            UNTIL 0AB0: 122

            0164: disable_marker 29@

            0A9B: closefile 31@
            0AF8: "{FF0000}Recorder:{FFFFFF} recording has been saved." -1
            REPEAT
                WAIT 0
            UNTIL  8AB0: 122 {O}               
        END */   
    
        IF
        8B21: NOT_CHAT_OPEN
        THEN
            IF OR
            30@ == TRUE
            0AB0: 123 {P}
            THEN
                REPEAT
                    WAIT 0
                UNTIL  8AB0: 123 {p}
                IF
                0AAB:   file_exists "CLEO\BusDriverPath.path"
                THEN
                    0AF8: "[Bus Driver]: Cheat-ul a fost {FFFF00}pornit{FFFFFF}." -1
                    
                    0A9A: 31@ = openfile "CLEO\BusDriverPath.path" mode "rb"
                    
                    REPEAT
                    WAIT 0
                        IF
                        80DF:   actor $PLAYER_ACTOR driving
                        THEN
                            0AF8: "[Bus Driver]: Cheat-ul a fost {FF0000}oprit (crash){FFFFFF}." -1
                            BREAK
                        END      
                        IF AND
                        8B21: NOT_CHAT_OPEN
                        0AB0: 123 {p}
                        THEN
                            BREAK
                        END                      
    
                        03C0: 0@ = actor $PLAYER_ACTOR car
    
                        0A9D: readfile 31@ size 4 to 1@
                        0A9D: readfile 31@ size 4 to 2@
                        0A9D: readfile 31@ size 4 to 3@
                        0A9D: readfile 31@ size 4 to 4@
                                                
                        REPEAT
                        WAIT 0
                            IF
                            80DF:   actor $PLAYER_ACTOR driving
                            THEN
                                0AF8: "[Bus Driver]: Rulare in bucla {FFFF00}dezactivata{FFFFFF} automat." -1
                                30@ = FALSE
                                // 0AF8: "{FF0000}Recorder:{FFFFFF} looping has been disabled." -1
                                BREAK
                            END                 
                            IF
                            0AB0: 123 {p}
                            THEN
                                30@ = FALSE
                                0AF8: "[Bus Driver]: Cheat-ul a fost {FF0000}oprit (nu conduci){FFFFFF}." -1
                                BREAK
                            END           
                            0AB1: @DRAW_TEXT_PLAYING 0         
                            0407: 6@ 7@ 8@ CAR 0@ w_OFFSET 0.0 0.0 0.0  
                            IF
                                key_down 103
                            THEN
                            
                            ELSE
                            
                                0AB1: call_scm_func @turning_mechanism 4 1@ 2@ 6@ 7@
                               
                            END  
                            
                            02E3: 5@ = car 0@ speed

                            // UPDATE 2.0 (bus wait via chat)


                            // cand intra in checkpoint
                            0AC8: 20@ = allocate_memory_size 260
                            0AC8: 21@ = allocate_memory_size 260 
                            0B75: samp get_chat_string 99 text_to 20@ prefix_to 21@ color_to $NOT_USED_VAR prefix_color_to $NOT_USED_VAR  

                            IF
                                0AD4: $NOT_USED = scan_string 20@ format "Opreste %s in checkpoint-ul rosu." 22@v
                            THEN
                                wait 0
                                
                                0ACD: show_text_highpriority "~Y~ASTEPT IN CHECKPOINT...~W~" time 5000
                                // 0AF8: "{FF0000}DEBUG:{FFFFFF} astept banii" -1
                                $WAIT_FOR_CASH = TRUE
                            END

                            // cand primeste banii
                            0AC8: 20@ = allocate_memory_size 260
                            0AC8: 21@ = allocate_memory_size 260 
                            0B75: samp get_chat_string 99 text_to 20@ prefix_to 21@ color_to $NOT_USED_VAR prefix_color_to $NOT_USED_VAR  

                            IF
                                0AD4: $NOT_USED = scan_string 20@ format "Ai primit %s. Mergi la urmatorul checkpoint." 22@v
                            THEN
                                wait 0
                                
                                $WAIT_FOR_CASH = FALSE
                                // 0AF8: "{FF0000}DEBUG:{FFFFFF} primit banii" -1
                                0ACD: show_text_highpriority "~G~MERG LA URMATORUL CHECKPOINT...~W~" time 5000
                                // say "%s" 22@v
                            END

                            IF
                                $DEBUG_DESIRED_SPEED == TRUE
                            THEN
                                0AF8: "[DEBUG] Desired speed: %.1f" 4@
                            END

                            WAIT 0

                            // END UPDATE 2.0
                            
                            // printf "%f" 10 5@
                            
                            // IF AND
                            //     0100:   actor $PLAYER_ACTOR in_sphere 1924.73 2316.08 10.77 radius 3.0 3.0 3.0 sphere 0 in_car 
                            //     5@ == 0
                            //     4@ == 0
                            // THEN
                            //     wait 500
                            // END
                            
                            IF AND
                                0100:   actor $PLAYER_ACTOR in_sphere 496.17 -1282.56 15.63 radius 7.0 7.0 7.0 sphere 0 in_car 
                                5@ > 6.0
                            THEN
                               0AB1: @BRAKE 0
                            END  
                            
                            IF
                                key_down 103
                            THEN
                            
                            ELSE
                            
                                IF
                                    0025:   5@ > 4@  // (float)
                                    // $WAIT_FOR_CASH == TRUE
                                THEN
                                    0AB1: @BRAKE 0
                                ELSE
                                    IF
                                        $WAIT_FOR_CASH == FALSE
                                    THEN
                                        0AB1: @GAS 0
                                    END
                                END
                            
                            END
                            
                            // limitarea vitezei (old - se verifica dupa cp)
                            // IF AND
                            //     5@ == 0  // (float)
                            //     4@ == 0
                            // THEN
                            //     wait 150 
                            // END
                              
                            0B55: convert_3D_coords 1@ 2@ 3@ to_screen 9@ 10@
                            0B55: convert_3D_coords 6@ 7@ 8@ to_screen 11@ 12@
                            if
                            00C2:   sphere_onscreen 1@ 2@ 3@ radius 5.0
                            then
                                0B68: render draw_line_point1 11@ 12@ point2 9@ 10@ width 2 color 0xFFFFFF00
                                0B70: render draw_polygon_pos 9@ 10@ size 10 10 corners 14 rotation 0 color 0xFFFFFF00
                                0B70: render draw_polygon_pos 11@ 12@ size 10 10 corners 14 rotation 0 color 0xFFFFFF00
                            end
                            
                        UNTIL 0100:   actor $PLAYER_ACTOR in_sphere 1@ 2@ 3@ radius 2.0 2.0 2.0 sphere 0 in_car 
                        
                    UNTIL 0AD6:   end_of_file 31@ reached                
         
                    0AF8: "[Bus Driver]: {FFFF00}Cheat-ul a terminat!{FFFFFF}" -1           
                    0A9B: closefile 31@
                    REPEAT
                        WAIT 0
                    UNTIL  8AB0: 123 {p}           
                    
                END
            END    
        END
    
    
        

        
    END
END






//0AB1: call_scm_func @turning_mechanism 4 XY XY
:turning_mechanism
0AB1: @getZAngleBetweenPoints 4 from_XY 0@ 1@ and_XY 2@ 3@ store_to 31@        
0172: 30@ = actor $PLAYER_ACTOR Z_angle
30@ -= 360.0
0097: make 30@ absolute_float
005B: 31@ += 30@
02F6: 30@ = sine 31@
02F7: 31@ = cosine 31@
0604: get_Z_angle_for_point 30@ 31@ store_to 31@
    if and
    0021:   31@ > 180.0
    0023:   355.0 > 31@
    then //press left
    0B56: set_game_key 0 state -255 
    //0B56: set_game_key 16 state 255
        if
        0023:   200.0 > 31@
        then
        0B56: set_game_key 6 state 255 
        else
        0B56: set_game_key 6 state 0
        end                           
    else
        if and
        0021:   31@ > 5.0
        0033:   180.0 >= 31@ //lower or equal  
        then //press right  
        0B56: set_game_key 0 state 255
        //0B56: set_game_key 16 state 255
            if
            0021:   31@ > 160.0
            then
            0B56: set_game_key 6 state 255 
            else
            0B56: set_game_key 6 state 0
            end 
        else
        0B56: set_game_key 0 state 0
        0B56: set_game_key 6 state 0
        //0B56: set_game_key 16 state 255
        end
    end
0AB2: ret 0

:getZAngleBetweenPoints
0063: 0@ -= 2@
0063: 1@ -= 3@
0604: get_Z_angle_for_point 0@ 1@ store_to 4@
0AB2: 1 4@

:DRAW_TEXT_PLAYING
if
00DF:   actor $PLAYER_ACTOR driving
then
    03C0: 0@ = actor $PLAYER_ACTOR car
    0407: 1@ 2@ 3@ CAR 0@ w_OFFSET 0.0 0.0 0.0   
    0BFD: 31@ = get_global_var "font"
    0B55: convert_3D_coords 1@ 2@ 3@ to_screen 4@ 5@
    0B6B: 6@ = FONT 31@ TEXT "BusDriver ON" LENGTH
    6@ /= 2
    0062: 4@ -= 6@
    0B6F: FONT 31@ DRAW_TEXT "BusDriver ON" POS 4@ 5@ COLOR 0xFFFFFF00 
end
0AB2: 0

:DRAW_TEXT_RECORDING
if
00DF:   actor $PLAYER_ACTOR driving
then
    03C0: 0@ = actor $PLAYER_ACTOR car
    0407: 1@ 2@ 3@ CAR 0@ w_OFFSET 0.0 0.0 0.0   
    0BFD: 31@ = get_global_var "font"
    0B55: convert_3D_coords 1@ 2@ 3@ to_screen 4@ 5@
    0B6B: 6@ = FONT 31@ TEXT "RECORDING" LENGTH
    6@ /= 2
    0062: 4@ -= 6@
    0B6F: FONT 31@ DRAW_TEXT "RECORDING" POS 4@ 5@ COLOR 0xFFFFFF00 
end
0AB2: 0

:BRAKE
0@ = 0xB73458 
0@ += 0xC
0A8C: write_memory 0@ size 1 value 255 virtual_protect 0
0AB2: 0

:GAS
0@ = 0xB73458 
0@ += 0x20 
0A8C: write_memory 0@ size 1 value 255 virtual_protect 0
0AB2: 0

:VehicleRecordingLoop
IF
30@ == TRUE
THEN
    30@ = FALSE
    0AF8: "[Bus Driver]: {FF0000}Ai oprit{FFFFFF} rularea in bucla..." -1              
ELSE
    30@ = TRUE
    0AF8: "[Bus Driver]: Rulare in bucla {FFFF00}activata{FFFFFF} by default." -1                 
    0AF8: "[Bus Driver]: Daca doresti sa opresti functia foloseste {FFFF00}/vloop{FFFFFF}." -1                 
END
0B43:

:VehicleRecording

0AF8: "------ Bus driver cheat - By {FFFF00}RewardK{FFFFFF} ------" -1
0AF8: "Creat pentru prima data in 2017, actualizat in 2026. Complet autonom, testat personal in sute de ore." -1
0AF8: "Foloseste CLEO & SAMPFUNCS. Consider ca Moonloader are prea mult acces, de unde se pot face multe scripturi malitioase." -1
0AF8: "------ {FFFF00}REZOLVAT BUGUL{FFFFFF} DE LA CHECKPOINT-UL 1 si 32" -1
0AF8: "Pe bugged exista un bug ce face sa dureze pana la 1 minut sa iei uneori un checkpoint (cine face bus driver stie)." -1
0AF8: "Acesta este rezolvat in acest cheat intrucat nu merge mai departe pana nu primeste pe chat [...]" -1
0AF8: "mesajul cum ca ai primit banii. Va astepta acolo cat este nevoie (uneori si 1-2 minute)." -1
0AF8: "{FFFF00}/startbusdriver{FFFFFF} - porneste cursa (dupa ce te-ai urcat in autobuz)." -1
0AF8: "{FFFF00}/vloop{FFFFFF} - opreste bucla (se opreste dupa ce termina tura actuala)." -1
0AF8: "Daca vrei control manual tine apasata tasta {FFFF00}NUMPAD 7{FFFFFF} si condu din WASD." -1
0AF8: "Se opreste automat cand te dai jos din autobuz. Daca vrei sa nu te dai jos, apasa tasta {FFFF00}F12{FFFFFF}!" -1
0AF8: "------" -1 

/* 0AF8: "{FF0000}Recorder:{FFFFFF} made by RewardK (bugged.ro)" -1
0AF8: "{FF0000}Recorder:{FFFFFF} to start/stop recording press 'O'." -1
0AF8: "{FF0000}Recorder:{FFFFFF} to start/stop playing recorded path press 'P'." -1
0AF8: "{FF0000}Recorder:{FFFFFF} write /vloop to start looping." -1 */

0B43:

:DebugDesiredSpeed

IF
    $DEBUG_DESIRED_SPEED == TRUE
THEN
    $DEBUG_DESIRED_SPEED = FALSE
ELSE
    $DEBUG_DESIRED_SPEED = TRUE
END

0B43: